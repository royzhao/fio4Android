# This Gnuplot file has been generated by eNovance

set title '$0'

set terminal png size 1280,1024
set output '$3.png'
#set terminal x11
#3D Config
set isosamples 30
set hidden3d
set pm3d at s solid hidden3d 100 scansbackward
set pm3d depthorder

#Preparing Axes
#set logscale x
set ytics axis out 0,1
#set data style lines
set grid back
set key top left reverse
set ylabel "Disk"
set xlabel "Time (Seconds)"
set zlabel '$4'
set cbrange [0:]
set zrange [0:]

#Set Color style
#set palette rgbformulae 22,9,23
set palette rgbformulae 7,5,15
set style line 100 lt 7 lw 0.5

#Multiploting
set multiplot

#Top Left View
set size 0.5,0.5
set view 64,216
set origin 0,0.5
splot '$1' using 2:1:3 with linespoints title '$2'

#Top Right View
set size 0.5,0.5
set origin 0.5,0.5
set view 90,0
set pm3d at s solid hidden3d 100 scansbackward
set pm3d depthorder
splot '$1' using 2:1:3 with linespoints title '$2'

#Bottom Right View
set size 0.5,0.5
set origin 0.5,0
set view 63,161
set pm3d at s solid hidden3d 100 scansbackward
set pm3d depthorder
splot '$1' using 2:1:3 with linespoints title '$2'

#Bottom Left View
set size 0.5,0.5
set origin 0,0
set pm3d map
splot '$1' using 2:1:3 with linespoints title '$2'

#Unsetting multiplotting
unset multiplot
#pause -1

#Preparing 3D Interactive view
set mouse
set terminal png size 1024,768
set output '$3-3D.png'

#set term x11
set view 64,216
set origin 0,0
set size 1,1
set pm3d at bs solid hidden3d 100 scansbackward
set pm3d depthorder
splot '$1' using 2:1:3 with linespoints title '$2'

#pause -1
#The End
